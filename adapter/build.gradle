plugins {
	id 'org.jetbrains.kotlin.jvm'
	id 'application'
	id 'com.jaredsburrows.license' version '0.8.42'
}

version = projectVersion

mainClassName = 'org.javacs.ktda.KDAMainKt'
description = 'Debug Adapter for Kotlin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

startScripts {
	applicationName = "kotlin-debug-adapter"
}

repositories {
	maven { url 'https://jitpack.io' }
}

dependencies {
	// The JSON-RPC and Debug Adapter Protocol implementations
	implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j.debug:0.10.0'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'com.github.fwcd.kotlin-language-server:shared:fb2c4f58aec6738ee2b66afb583ab51e2c424ebb'
	// The Java Debug Interface classes (com.sun.jdi.*)
	implementation files("${System.properties['java.home']}/../lib/tools.jar")
	// For CommandLineUtils.translateCommandLine
	implementation 'org.codehaus.plexus:plexus-utils:3.3.0'
	testImplementation 'junit:junit:4.12'
	testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

task fixFilePermissions(type: Exec) {
	// When running on macOS or Linux the start script
	// needs executable permissions to run.
	onlyIf { !System.getProperty('os.name').toLowerCase().contains('windows') }
	commandLine 'chmod', '+x', "${installDist.destinationDir}/bin/kotlin-debug-adapter"
}

test {
	testLogging {
		events 'failed'
		exceptionFormat 'short'
	}
}

run {
	standardInput = System.in
}

distZip {
	archiveFileName = "${project.name}.zip"
}

installDist.finalizedBy fixFilePermissions
